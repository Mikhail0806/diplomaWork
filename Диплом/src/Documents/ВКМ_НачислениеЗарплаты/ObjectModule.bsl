#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
	
#Область ОбработчикиСобытий
	
Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	СформироватьДвижения();
	РассчитатьОклад();
	СформироватьДвиженияПроцентПермии(НачалоМесяца(Дата), КонецМесяца(Дата));
	РассчитатьОтпуск();
	СформирватьУдержание();
	СформироватьВзаиморасчетыССотрудниками();
	СформироватьУчетОтпуска();

КонецПроцедуры
	
#КонецОбласти
	
	
	
#Область СлужебныеПроцедурыИФункции
	
Процедура СформироватьДвижения()  
	
	СформироватьДвиженияОсновныеНачисления(); 
	
	СформироватьСторноЗаписи();	
	Движения.ВКМ_ОсновныеНачисления.Записать(); 
	
КонецПроцедуры

Процедура СформироватьДвиженияОсновныеНачисления()

	Для Каждого Строка Из Начисления Цикл

		Если Строка.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.ПремияПроцент Тогда

			Продолжить;

		КонецЕсли;

		Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Строка);
		Движение.ПериодДействияНачало = Строка.ДатаНачала;
		Движение.ПериодДействияКонец = КонецДня(Строка.ДатаОкончания);
		Движение.ПериодРегистрации = Дата;
		
		//Установка периода расчета отпуска
		Если Строка.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск Тогда

			Движение.БазовыйПериодНачало = НачалоМесяца(ДобавитьМесяц(Строка.ДатаНачала, -12));
			Движение.БазовыйПериодКонец = КонецМесяца(ДобавитьМесяц(Строка.ДатаОкончания, -1));

		КонецЕсли;
	КонецЦикла;

	Движения.ВКМ_ОсновныеНачисления.Записать();

КонецПроцедуры

Процедура СформироватьСторноЗаписи()
	
	СторноЗаписи = Движения.ВКМ_ОсновныеНачисления.ПолучитьДополнение();
	
	Для Каждого Строка Из СторноЗаписи Цикл
		
		Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Строка);
		
		Движение.ПериодРегистрации = Дата;
		Движение.ПериодДействияНачало = Строка.ПериодДействияНачалоСторно;
		Движение.ПериодДействияКонец = Строка.ПериодДействияКонецСторно; 
		Движение.Сторно = Истина;
		
	КонецЦикла;
	
КонецПроцедуры  

Процедура РассчитатьОклад()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
	|	ЕСТЬNULL(ВКМ_УсловияОплатыСотрудниковСрезПоследних.Оклад, 0) КАК Оклад,
	|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияДанныеГрафика.РабочихДнейФактическийПериодДействия, 0) КАК Факт,
	|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияДанныеГрафика.РабочихДнейПериодДействия, 0) КАК План
	|ИЗ
	|	РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(ВидРасчета = &Оклад
	|	И Регистратор = &Ссылка) КАК ВКМ_ОсновныеНачисленияДанныеГрафика
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&Период, Сотрудник В
	|			(ВЫБРАТЬ
	|				ВКМ_НачислениеЗарплатыНачисления.Сотрудник КАК Сотрудник
	|			ИЗ
	|				Документ.ВКМ_НачислениеЗарплаты.Начисления КАК ВКМ_НачислениеЗарплатыНачисления
	|			ГДЕ
	|				ВКМ_НачислениеЗарплатыНачисления.ВидРасчета = &Оклад
	|				И ВКМ_НачислениеЗарплатыНачисления.Ссылка = &Ссылка)) КАК ВКМ_УсловияОплатыСотрудниковСрезПоследних
	|		ПО ВКМ_ОсновныеНачисленияДанныеГрафика.Сотрудник = ВКМ_УсловияОплатыСотрудниковСрезПоследних.Сотрудник";
	
	Запрос.УстановитьПараметр("Оклад", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад);
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ВКМ_ОсновныеНачисления[Выборка.НомерСтроки - 1];
		
		Если Выборка.План = 0 Тогда   
			
			Движение.Результат = 0; 
			
		Иначе
			
			Движение.Результат = Выборка.Оклад * Выборка.Факт / Выборка.План;
			Движение.ОтработаноДней = Выборка.Факт;
			Движение.Показатель = Выборка.Оклад;
			
		КонецЕсли; 
		
		Если Движение.Сторно Тогда  
			
			Движение.Результат = - Движение.Результат;
			Движение.ОтработаноДней = - Движение.ОтработаноДней;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Движения.ВКМ_ОсновныеНачисления.Записать(, Истина);	
	
КонецПроцедуры
 
 Процедура СформироватьДвиженияПроцентПермии(ДатаНачала, ДатаОкончание)

	Движения.ВКМ_ОсновныеНачисления.Записывать = Истина;

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВКМ_НачислениеЗарплатыНачисления.Сотрудник,
	|	ВКМ_НачислениеЗарплатыНачисления.ВидРасчета,
	|	ВКМ_НачислениеЗарплатыНачисления.ДатаНачала,
	|	ВКМ_НачислениеЗарплатыНачисления.ДатаОкончания,
	|	ВКМ_НачислениеЗарплатыНачисления.ГрафикРаботы,
	|	ВКМ_НачислениеЗарплатыНачисления.Ссылка
	|ПОМЕСТИТЬ ВТ_ТЧ
	|ИЗ
	|	Документ.ВКМ_НачислениеЗарплаты.Начисления КАК ВКМ_НачислениеЗарплатыНачисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&Дата, Сотрудник В
	|			(ВЫБРАТЬ
	|				ВКМ_НачислениеЗарплатыНачисления.Сотрудник КАК Сотрудник
	|			ИЗ
	|				Документ.ВКМ_НачислениеЗарплаты.Начисления КАК ВКМ_НачислениеЗарплатыНачисления
	|			ГДЕ
	|				ВКМ_НачислениеЗарплатыНачисления.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.ВКМ_ОсновныеНачисления.ПремияПроцент)
	|				И ВКМ_НачислениеЗарплатыНачисления.Ссылка = &Ссылка)
	|		И ПроцентОтРабот <> 0) КАК ВКМ_УсловияОплатыСотрудниковСрезПоследних
	|		ПО ВКМ_НачислениеЗарплатыНачисления.Сотрудник = ВКМ_УсловияОплатыСотрудниковСрезПоследних.Сотрудник
	|ГДЕ
	|	ВКМ_НачислениеЗарплатыНачисления.Ссылка = &Ссылка
	|	И ВКМ_НачислениеЗарплатыНачисления.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.ВКМ_ОсновныеНачисления.ПремияПроцент)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ВКМ_ВыполненныеСотрудникомРаботыОбороты.СуммаКОплатеОборот, 0) КАК СуммаКОплате,
	|	ВТ_ТЧ.Сотрудник,
	|	ВТ_ТЧ.ВидРасчета,
	|	ВТ_ТЧ.ДатаНачала,
	|	ВТ_ТЧ.ДатаОкончания,
	|	ВТ_ТЧ.ГрафикРаботы,
	|	ВТ_ТЧ.Ссылка,
	|	ЕСТЬNULL(ВКМ_ВыполненныеСотрудникомРаботыОбороты.ЧасовКОпалтеОборот, 0) КАК ЧасовКОпалтеОборот
	|ИЗ
	|	ВТ_ТЧ КАК ВТ_ТЧ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВКМ_ВыполненныеСотрудникомРаботы.Обороты(&ДатаНачала, &ДатаОкончание,, Сотрудник
	|			В
	|			(ВЫБРАТЬ
	|				ВКМ_НачислениеЗарплатыНачисления.Сотрудник КАК Сотрудник
	|			ИЗ
	|				Документ.ВКМ_НачислениеЗарплаты.Начисления КАК ВКМ_НачислениеЗарплатыНачисления
	|			ГДЕ
	|				ВКМ_НачислениеЗарплатыНачисления.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.ВКМ_ОсновныеНачисления.ПремияПроцент)
	|				И ВКМ_НачислениеЗарплатыНачисления.Ссылка = &Ссылка)) КАК ВКМ_ВыполненныеСотрудникомРаботыОбороты
	|		ПО ВТ_ТЧ.Сотрудник = ВКМ_ВыполненныеСотрудникомРаботыОбороты.Сотрудник";

	Запрос.УстановитьПараметр("ДатаОкончание", ДатаОкончание);
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Дата",Дата);

	РезультатЗапроса = Запрос.Выполнить();

	Выборка = РезультатЗапроса.Выбрать();

	Пока Выборка.Следующий() Цикл
		
		Если Выборка.СуммаКОплате = 0 Тогда
			Продолжить;
		КонецЕсли;

		Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();

		ЗаполнитьЗначенияСвойств(Движение, Выборка);
		Движение.Регистратор = Выборка.Ссылка;
		Движение.ПериодДействияНачало = Выборка.ДатаНачала;
		Движение.ПериодДействияКонец = КонецДня(Выборка.ДатаОкончания);
		Движение.ПериодРегистрации = Дата;
		Движение.Показатель = Выборка.ЧасовКОпалтеОборот;
		Движение.Результат = Выборка.СуммаКОплате;

	КонецЦикла;

	Движения.ВКМ_ОсновныеНачисления.Записать();

КонецПроцедуры

Процедура РассчитатьОтпуск()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВКМ_ОсновныеНачисления.НомерСтроки КАК НомерСтроки,
	|	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.РабочихДнейФактическийПериодДействия, 0) КАК Факт,
	|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ДополнительныеНачисления.СуммаКВыплатеБаза, 0) КАК БазаДоп,
	|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.ОтработаноДнейБаза, 0) КАК БазаОтработано,
	|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.РезультатБаза, 0) КАК БазаОсн
	|ИЗ
	|	РегистрРасчета.ВКМ_ОсновныеНачисления КАК ВКМ_ОсновныеНачисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(
	|				Регистратор = &Ссылка
	|					И ВидРасчета = &Отпуск) КАК ОсновныеНачисленияДанныеГрафика
	|		ПО ВКМ_ОсновныеНачисления.НомерСтроки = ОсновныеНачисленияДанныеГрафика.НомерСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.БазаВКМ_ДополнительныеНачисления(
	|				&Измерения,
	|				&Измерения,
	|				,
	|				Регистратор = &Ссылка
	|					И ВидРасчета = &Отпуск) КАК ВКМ_ОсновныеНачисленияБазаВКМ_ДополнительныеНачисления
	|		ПО ВКМ_ОсновныеНачисления.НомерСтроки = ВКМ_ОсновныеНачисленияБазаВКМ_ДополнительныеНачисления.НомерСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.БазаВКМ_ОсновныеНачисления(
	|				&Измерения,
	|				&Измерения,
	|				,
	|				ВидРасчета = &Отпуск
	|					И Регистратор = &Ссылка) КАК ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления
	|		ПО ВКМ_ОсновныеНачисления.НомерСтроки = ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.НомерСтроки
	|ГДЕ
	|	ВКМ_ОсновныеНачисления.Регистратор = &Ссылка
	|	И ВКМ_ОсновныеНачисления.ВидРасчета = &Отпуск";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);	
	Запрос.УстановитьПараметр("Отпуск", ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск);
	
	Измерения = Новый Массив;
	Измерения.Добавить("Сотрудник");
	
	Запрос.УстановитьПараметр("Измерения", Измерения);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ВКМ_ОсновныеНачисления[Выборка.НомерСтроки - 1];         
		
		Если Выборка.БазаОтработано = 0 Тогда  
			
			Движение.Результат = 0;
			Продолжить;
			
		КонецЕсли;
		
		Движение.Результат = (Выборка.БазаОсн + Выборка.БазаДоп) * Выборка.Факт / Выборка.БазаОтработано;
		Движение.Показатель = Выборка.БазаОсн + Выборка.БазаДоп;
		Движение.ДнейОтпуска = Выборка.Факт;
		
		Если Движение.Сторно Тогда  
			
			Движение.Результат = - Движение.Результат;
			Движение.ОтработаноДней = - Движение.ОтработаноДней;
			
		КонецЕсли;
					
	КонецЦикла;	
	
	Движения.ВКМ_ОсновныеНачисления.Записать(, Истина);
	
КонецПроцедуры

Процедура СформирватьУдержание()
	
	Движения.ВКМ_Удержания.Записывать = Истина;
	
	НДФЛ = Константы.ВКМ_НДФЛ.Получить();
	Если ЗначениеЗаполнено(НДФЛ) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВКМ_ОсновныеНачисления.Сотрудник КАК Сотрудник,
		|	СУММА(ВКМ_ОсновныеНачисления.Результат) КАК Результат
		|ИЗ
		|	РегистрРасчета.ВКМ_ОсновныеНачисления КАК ВКМ_ОсновныеНачисления
		|ГДЕ
		|	ВКМ_ОсновныеНачисления.Регистратор = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ВКМ_ОсновныеНачисления.Сотрудник";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл 
			
			Движение = Движения.ВКМ_Удержания.Добавить();
			Движение.Сотрудник = Выборка.Сотрудник;
			Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.НДФЛ;
			Движение.БазовыйПериодНачало = НачалоМесяца(Дата);
			Движение.БазовыйПериодКонец = КонецМесяца(КонецДня(Дата));
			Движение.ПериодРегистрации = Дата;
			Движение.Показатель = Выборка.Результат;
			Движение.Результат = Выборка.Результат * (НДФЛ / 100);
			
			Движения.ВКМ_Удержания.Записать();
		КонецЦикла;
		
	Иначе 
		
		Сообщение = "Не установлен процент НДФЛ";
		ОбщегоНазначения.СообщитьПользователю(Сообщение);
		
	КонецЕсли;
	
КонецПроцедуры 

Процедура СформироватьВзаиморасчетыССотрудниками() 
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВКМ_Удержания.Сотрудник КАК Сотрудник,
	|	ЕСТЬNULL(ВКМ_Удержания.Результат, 0) КАК Удержание,
	|	СУММА(ЕСТЬNULL(ВКМ_ОсновныеНачисления.Результат, 0)) КАК результат
	|ИЗ
	|	РегистрРасчета.ВКМ_ОсновныеНачисления КАК ВКМ_ОсновныеНачисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_Удержания КАК ВКМ_Удержания
	|		ПО ВКМ_ОсновныеНачисления.Сотрудник = ВКМ_Удержания.Сотрудник
	|ГДЕ
	|	ВКМ_Удержания.Регистратор = &Ссылка
	|	И ВКМ_ОсновныеНачисления.Регистратор = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ВКМ_Удержания.Сотрудник,
	|	ЕСТЬNULL(ВКМ_Удержания.Результат, 0)";
	
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл       
		
		Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.ДобавитьПриход();
		Движение.Период = Дата;
		Движение.Сотрудник = Выборка.Сотрудник;
		Движение.Сумма = Выборка.Результат - Выборка.Удержание;    
		
	КонецЦикла;

	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
	
КонецПроцедуры 

Процедура СформироватьУчетОтпуска()  
	
	
	Движения.ВКМ_УчетОтпуска.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВКМ_ОсновныеНачисления.Сотрудник КАК Сотрудник,
		|	ЕСТЬNULL(ВКМ_ОсновныеНачисления.ДнейОтпуска, 0) КАК ДнейОтпуска,
		|	ВКМ_ОсновныеНачисления.ПериодРегистрации КАК ПериодРегистрации
		|ИЗ
		|	РегистрРасчета.ВКМ_ОсновныеНачисления КАК ВКМ_ОсновныеНачисления
		|ГДЕ
		|	ВКМ_ОсновныеНачисления.ВидРасчета = ЗНАЧЕНИЕ(ПланВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск)
		|	И ВКМ_ОсновныеНачисления.Регистратор = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка= РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		 Движение = Движения.ВКМ_УчетОтпуска.ДобавитьРасход();
		 Движение.Год = Выборка.ПериодРегистрации;
		 Движение.Период = Выборка.ПериодРегистрации;
		 Движение.ДнейОтпсука = Выборка.ДнейОтпуска;
		 Движение.Сотрудник = Выборка.Сотрудник;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
