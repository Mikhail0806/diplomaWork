#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда    
	
	#Область СлужебныеПроцедурыИФункции
	Процедура ОбработкаПроведения(Отказ, Режим)
		
		Движения.ВКМ_ДополнительныеНачисления.Записывать = Истина;
		Движения.ВКМ_Удержания.Записывать = Истина;
		Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
		
		НДФЛ = Константы.ВКМ_НДФЛ.Получить();
		
		Если Не ЗначениеЗаполнено(НДФЛ) Тогда 
			
			Сообщение = "Не установлен процент НДФЛ";
			ОбщегоНазначения.СообщитьПользователю(Сообщение);
			Отказ = Истина;
			Возврат;
			
		КонецЕсли;
		
		Для Каждого Строка Из СписокСотрудников Цикл
			
			//Движение  ДополнительныеНачисления
			Движение = Движения.ВКМ_ДополнительныеНачисления.Добавить();
			Движение.Сторно = Ложь;
			Движение.ВидРасчета = ВидРасчета;
			Движение.ПериодРегистрации = Дата;
			Движение.БазовыйПериодНачало = Строка.ДатаНачала;
			Движение.БазовыйПериодКонец = Строка.ДатаОкончания;
			Движение.Сотрудник = Строка.Сотрудник;
			Движение.СуммаКВыплате = Строка.СуммаПремии;
			
			//Движение Удержание
			Движение = Движения.ВКМ_Удержания.Добавить();
			Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.НДФЛ;
			Движение.ПериодРегистрации = Дата;
			Движение.БазовыйПериодНачало = Строка.ДатаНачала;
			Движение.БазовыйПериодКонец = Строка.ДатаОкончания;
			Движение.Сотрудник = Строка.Сотрудник;
			Движение.Показатель = Строка.СуммаПремии;
			СуммаНДФЛ = Строка.СуммаПремии * (НДФЛ / 100);
			Движение.Результат = СуммаНДФЛ;
			
			//Движение ВзаиморасчетыССотрудниками
			Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.ДобавитьПриход();
			Движение.Период = Дата;
			Движение.Сотрудник = Строка.Сотрудник;
			Движение.Сумма = Строка.СуммаПремии - СуммаНДФЛ;			
			
		КонецЦикла;
		
	КонецПроцедуры 
	
	#КонецОбласти
	
#КонецЕсли

