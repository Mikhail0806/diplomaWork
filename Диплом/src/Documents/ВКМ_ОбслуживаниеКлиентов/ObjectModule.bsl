#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
#Область СлужебныеПроцедурыИФункции 

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.ВКМ_ВыполненныеКлиентуРаботы.Записывать = Истина;
	Движения.ВКМ_ВыполненныеСотрудникомРаботы.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВКМ_ОбслуживаниеКлиентовВыполненныеРаботы.Ссылка.Договор.ВКМ_СтоимостьЧасаРаботы КАК СтоимостьЧасаРаботы,
	|	ВКМ_ОбслуживаниеКлиентовВыполненныеРаботы.ЧасыКОплатеКлиенту КАК ЧасыКОплатеКлиенту,
	|	ВКМ_ОбслуживаниеКлиентовВыполненныеРаботы.Ссылка.Специалист КАК Специалист
	|ПОМЕСТИТЬ ВТ_ТЧ
	|ИЗ
	|	Документ.ВКМ_ОбслуживаниеКлиентов.ВыполненныеРаботы КАК ВКМ_ОбслуживаниеКлиентовВыполненныеРаботы
	|ГДЕ
	|	ВКМ_ОбслуживаниеКлиентовВыполненныеРаботы.Ссылка = &Ссылка
	|	И ВКМ_ОбслуживаниеКлиентовВыполненныеРаботы.Ссылка.Договор.ВидДоговора = &АбонентскаяПлата
	|	И ВКМ_ОбслуживаниеКлиентовВыполненныеРаботы.Ссылка.Договор.ВКМ_ДатаНачалаДоговора <= ВКМ_ОбслуживаниеКлиентовВыполненныеРаботы.Ссылка.Дата
	|	И ВКМ_ОбслуживаниеКлиентовВыполненныеРаботы.Ссылка.Договор.ВКМ_ДатаОкончанияДоговора >= ВКМ_ОбслуживаниеКлиентовВыполненныеРаботы.Ссылка.Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТЧ.СтоимостьЧасаРаботы КАК СтоимостьЧасаРаботы,
	|	ВТ_ТЧ.ЧасыКОплатеКлиенту КАК ЧасыКОплатеКлиенту,
	|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.Оклад КАК Оклад,
	|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.ПроцентОтРабот КАК ПроцентОтРабот
	|ИЗ
	|	ВТ_ТЧ КАК ВТ_ТЧ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&Дата, Сотрудник = &Специалист) КАК ВКМ_УсловияОплатыСотрудниковСрезПоследних
	|		ПО ВТ_ТЧ.Специалист = ВКМ_УсловияОплатыСотрудниковСрезПоследних.Сотрудник";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("АбонентскаяПлата",Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание);
	Запрос.УстановитьПараметр("Дата",Дата);
	Запрос.УстановитьПараметр("Специалист",Специалист);
	
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл    
		
		Движение = Движения.ВКМ_ВыполненныеКлиентуРаботы.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Клиент = Клиент;
		Движение.Договор = Договор;
		Движение.КоличествоЧасов = Выборка.ЧасыКОплатеКлиенту;
		Движение.СуммаКОплате = Выборка.ЧасыКОплатеКлиенту * Выборка.СтоимостьЧасаРаботы;
		
		
		Если НЕ ЗначениеЗаполнено(Выборка.ПроцентОтРабот) Тогда   
			
			Сообщение = СтрШаблон("Отмена проведения. Специалисту %1 не установленен процент премии за выполенные работы на дату документа: %2",
			Специалист,Дата);
			ОбщегоНазначения.СообщитьПользователю(Сообщение);
			Отказ = Истина;
			Возврат;
			
		КонецЕсли;
		
		Движение = Движения.ВКМ_ВыполненныеСотрудникомРаботы.Добавить();
		Движение.Период = Дата;
		Движение.Сотрудник = Специалист;
		Движение.ЧасовКОпалте = Выборка.ЧасыКОплатеКлиенту;
		Движение.СуммаКОплате = Выборка.ЧасыКОплатеКлиенту * Выборка.СтоимостьЧасаРаботы * Выборка.ПроцентОтРабот / 100; 
		
	КонецЦикла;
	
КонецПроцедуры


#КонецОбласти  
#КонецЕсли



